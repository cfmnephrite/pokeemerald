const OBJ_EVENT_ID_PROF_BIRCH		=	2
const OBJ_EVENT_ID_LINK_RS_MAY		=	3

mapscripts LittlerootTown_ProfessorBirchsLab_MapScripts {
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_LITTLEROOT_INTRO_STATE, 2: LittlerootTown_ProfessorBirchsLab_Script_Intro
	]
	MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
		VAR_LITTLEROOT_INTRO_STATE, 2: LittlerootTown_ProfessorBirchsLab_FacePlayerNorth
	]
}

script LittlerootTown_ProfessorBirchsLab_FacePlayerNorth {
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
	end
}

script LittlerootTown_ProfessorBirchsLab_Script_Intro {
	lockall
	setvar(VAR_LITTLEROOT_INTRO_STATE, 3)
	msgbox(LittlerootTown_ProfessorBirchsLab_ThankYouSoMuch, MSGBOX_DEFAULT)
	waitmessage
	playfanfare(MUS_OBTAIN_ITEM)
	waitfanfare
	msgbox(format("Would you like to give a nickname to Pichu?"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == YES) {
		setvar(VAR_0x8004, 0)
		call(Common_EventScript_NameReceivedPartyMon)
	}
	goto(LittlerootTown_ProfessorBirchsLab_ButFirst)
	end
}

script LittlerootTown_ProfessorBirchsLab_ButFirst {
	msgbox(LittlerootTown_ProfessorBirchsLab_IHaveAJobForYou, MSGBOX_DEFAULT)
	addobject(OBJ_EVENT_ID_LINK_RS_MAY)
	msgbox("Daaaad!")
	applymovement(OBJ_EVENT_ID_LINK_RS_MAY, LittlerootTown_ProfessorBirchsLab_Movement_MayEnter)
	waitmovement(0)
	msgbox(LittlerootTown_ProfessorBirchsLab_GetRivalToTestYou, MSGBOX_DEFAULT)
	applymovement(OBJ_EVENT_ID_PLAYER, LittlerootTown_ProfessorBirchsLab_Movement_FacingMay)
	setvar(VAR_CONTINUE_AFTER_LOSING_BATTLE, 1)
	trainerbattle_no_intro(TRAINER_MAY_ROUTE_103_TORCHIC, LittlerootTown_ProfessorBirchsLab_MayDefeat)
	specialvar(VAR_RESULT, GetBattleOutcome)
	if (var(VAR_RESULT) == B_OUTCOME_WON) {
		msgbox(LittlerootTown_ProfessorBirchsLab_NotBad, MSGBOX_DEFAULT)
	} else {
		msgbox(LittlerootTown_ProfessorBirchsLab_Disappointing, MSGBOX_DEFAULT)
	}
	goto(LittlerootTown_ProfessorBirchsLab_PostMayBattle)
	end
}

script LittlerootTown_ProfessorBirchsLab_PostMayBattle {
	applymovement(OBJ_EVENT_ID_PROF_BIRCH, LittlerootTown_ProfessorBirchsLab_Movement_BirchFetchPokedex)
	waitmovement(0)
	playfanfare(MUS_OBTAIN_ITEM)
	msgbox("{PLAYER} received the Pokédex!")
	waitfanfare
	msgbox(LittlerootTown_ProfessorBirchsLab_Congrats, MSGBOX_DEFAULT)
	giveitem(ITEM_POKE_BALL, 5)
	msgbox(LittlerootTown_ProfessorBirchsLab_TheseArePokeBalls, MSGBOX_DEFAULT)
	releaseall
	end
}

script LittlerootTown_ProfessorBirchsLab_RatePokedex {
	lock
	faceplayer
	goto(ProfBirch_EventScript_AskRatePokedex)
	end
}

script LittlerootTown_ProfessorBirchsLab_EventScript_Machine {
	msgbox(LittlerootTown_ProfessorBirchsLab_SeriousLookingMachine, MSGBOX_SIGN)
	end
}

script LittlerootTown_ProfessorBirchsLab_EventScript_Book {
	msgbox("It's a book too hard to read.", MSGBOX_SIGN)
	end
}

script LittlerootTown_ProfessorBirchsLab_EventScript_Bookshelf {
	msgbox("It's crammed with books on monsters.", MSGBOX_SIGN)
	end
}

script LittlerootTown_ProfessorBirchsLab_EventScript_PC {
	msgbox(LittlerootTown_ProfessorBirchsLab_EventScript_PCUsedForResearch, MSGBOX_SIGN)
	end
}

movement LittlerootTown_ProfessorBirchsLab_Movement_MayEnter {
	walk_up * 6
	step_end
}

movement LittlerootTown_ProfessorBirchsLab_Movement_FacingMay {
	face_down
	step_end
}

movement LittlerootTown_ProfessorBirchsLab_Movement_BirchFetchPokedex {
	walk_up *2
	walk_down *2
	step_end
}

text LittlerootTown_ProfessorBirchsLab_ThankYouSoMuch {
	format(
		"Birch: {PLAYER}, thank you so much for what you did last night. There are no words with which I can express my gratitude.\p"
		"I decided to let you keep that special Pichu. I have a feeling you'll get along with it really well!")
}

text LittlerootTown_ProfessorBirchsLab_IHaveAJobForYou {
	format(
		"Birch: Ah yes, the other thing I wanted to mention... There is a job I would like to offer you. I need someone to help me gather information on all monsters in the region. You would take this Pokedex and travel around Hoenn catching them, analysing them, registering their information here! But first, howewer...")
}

text LittlerootTown_ProfessorBirchsLab_GetRivalToTestYou {
	format(
		"Birch: Oh, May! Great timing... {PLAYER}, I would like you to have a battle with my daughter. Last night was indeed impressive, but I want to make sure it wasn't all just a fluke. Consider it a test to see if you're up to the task!")
}

text LittlerootTown_ProfessorBirchsLab_May_Intro {
	format(
		"May: That's right! I've been itching to have a battle with you since I saw what you did last night! C'mon, after beating down those thugs, you shouldn't be afraid to battle me... Right?")
}

text LittlerootTown_ProfessorBirchsLab_MayDefeat {
	format(
		"WHAT? You... {PLAYER}, you're really good!")
}

text LittlerootTown_ProfessorBirchsLab_NotBad {
	format(
		"Birch: That was a great battle, {PLAYER}! I didn't expect any less. Now, as I promised, I shall give you a Pokédex!")
}

text LittlerootTown_ProfessorBirchsLab_Congrats {
	format(
		"Birch: This Pokedex is a high-tech device that registers data of any monster you catch. It is actually pretty straightforward, you should find it easy to use.\p"
		"May: Kudos for winning, {PLAYER}! I gotta admit I have underestimated you... As a reward, here's a little gift!")
}

text LittlerootTown_ProfessorBirchsLab_Disappointing {
	format(
		"Birch: Oh... that was disappointing, {PLAYER}. Must I suppose that performance last night was only dumb luck? Still, I really need help for my research, so I can't be too picky. I'll give you the Pokédex anyways.")
}

text LittlerootTown_ProfessorBirchsLab_Hehehehe {
	format(
		"Birch: This Pokedex is a high-tech device that registers data of any monster you catch. It is extremely easy to use, even a baby should understand how it works.\p"
		"May: Hehehehe, don't feel too bad, {PLAYER}! It's just that I'm too strong for you! Here's a little gift because I feel sorry for you!")
}

text LittlerootTown_ProfessorBirchsLab_TheseArePokeBalls {
	format(
		"May: These are Poké Balls! They are used for catching wild Pokémon! You need to weaken your target a little bit first to make 'em easier to catch.")
}

text LittlerootTown_ProfessorBirchsLab_SeriousLookingMachine {
	format(
		"Wow, Core 2 Duo?\n4 gigs of RAM?\pIt's a serious looking machine. Birch must use it for research.")
}

text LittlerootTown_ProfessorBirchsLab_EventScript_PCUsedForResearch {
	format(
		"It's a PC used for research. Better not mess with it.")
}